apply plugin: 'com.android.application'
apply plugin: 'jacoco'

android {
    // API level 21 (Lollipop 5.0) deprecates Social Stream API and breakes ThumbActivity and NotifierService
    // if you find out a workaround also comment in http://stackoverflow.com/questions/31099037/replacement-for-contactscontract-streamitems-in-android-api-21
    compileSdkVersion 20
    buildToolsVersion "23.0.0"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 19
        versionCode 1
        versionName "1.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        release {
            minifyEnabled true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }

        debug {
            debuggable true
        }
    }

    // temporary disable lint until issues are fixed
    // it is rally only temporary I promise
    lintOptions {
        abortOnError false
    }
}

task jacocoTestReport(type: JacocoReport) {
    group = "Reporting"
    description = "Generate Jacoco coverage reports for Coveralls"
    reports {
        xml.enabled = true
        html.enabled = false
    }
    classDirectories = fileTree(
            dir: 'build/intermediates/classes/debug',
            excludes: ['**/jjnford/*',
                       '**/R*.class',
                       '**/*$InjectAdapter.class',
                       '**/*$ModuleAdapter.class',
                       '**/*$ViewInjector*.class'
            ])
    sourceDirectories = files('src/main/java')
    executionData = files("$buildDir/jacoco/testDebugUnitTest.exec")
    // Bit hacky but fixes https://code.google.com/p/android/issues/detail?id=69174.
    // We iterate through the compiled .class tree and rename $$ to $.
    doFirst {
        new File("$buildDir/intermediates/classes/").eachFileRecurse { file ->
            if (file.name.contains('$$')) {
                file.renameTo(file.path.replace('$$', '$'))
            }
        }
    }
}

dependencies {
    compile 'org.apache.commons:commons-lang3:+'
    compile 'com.facebook.android:facebook-android-sdk:4.10.1'
    // using fixed version to avoid breakages
    compile 'com.github.amlcurran.showcaseview:library:5.+'
    compile 'me.villani.lorenzo.android:android-cropimage:1.+'
    compile 'net.sf.biweekly:biweekly:0.+'
    compile 'com.github.yukuku:ambilwarna:2.+'

    testCompile 'junit:junit:4.+'
    testCompile 'org.mockito:mockito-core:1.+'
    testCompile 'org.robolectric:robolectric:3.+'
}